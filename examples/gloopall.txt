precision highp float;

#define M_PI 3.1415926535897932384626433832795
varying vec2 fragCoord;
uniform highp float iGlobalTime;
uniform vec3 iResolution;

// (((((x - xc1)**2) + ((y - yc1)**2) - (r1**2)) * (((x - xc2)**2) + ((y - yc2)**2) - (r2**2)))) < (s / 1000)

const vec3 position = vec3(-0.2, 0.2, 1.3);
const vec3 diffuseColour = vec3(0.25, 0.25, 0.5);
const float diffusePower = 2.0;
const vec3 specularColour = vec3(0.5, 0.1, 0.1);
const float specularPower = 5.0;
const float specularHardness = 5.0;
const vec3 ambientColour = vec3(0.4, 0.5, 0.4);

vec3 BlinnPhongLighting(vec3 pos, vec3 viewDir, vec3 normal) {
	vec3 lightDir = position - pos;
	float distance = length (lightDir);
	lightDir = lightDir / distance;
	distance = distance * distance;

	float NdotL = dot (normal, lightDir);
	float intensity = clamp (NdotL, 0.0, 1.0);
	vec3 diffuse = intensity * diffuseColour * diffusePower / distance;
	vec3 H = normalize (lightDir + viewDir);
	float NdotH = dot (normal, H);
	intensity = pow (clamp (NdotH, 0.0, 1.0), specularHardness);
	vec3 specular = intensity * specularColour * specularPower; 

	return (diffuse + specular + ambientColour);
}

void main(void) {
	const float stickiness = 0.0015;
	const float r1 = 0.25;
	const float r2 = 0.25;
	const float r3 = 0.25;
	vec2 ratio = vec2(iResolution.x, iResolution.y) / min(iResolution.x, iResolution.y);
	vec2 pos1 = vec2((1.0 + sin(iGlobalTime / 9.000)) / 2.0, (1.0 + sin(iGlobalTime / 7.1000)) / 2.0) * ratio;
	vec2 pos2 = vec2((1.0 + sin(iGlobalTime / 8.900)) / 2.0, (1.0 + sin(iGlobalTime / 10.400)) / 2.0) * ratio;
	vec2 pos3 = vec2((1.0 + sin(iGlobalTime / 9.650)) / 2.0, (1.0 + sin(iGlobalTime / 91.500)) / 2.0) * ratio;
	vec2 pos = fragCoord * ratio / iResolution.xy;

	float d1 = pow((pos.x - pos1.x), 2.0) + pow((pos.y - pos1.y), 2.0) - pow(r1, 2.0);
	float d2 = pow((pos.x - pos2.x), 2.0) + pow((pos.y - pos2.y), 2.0) - pow(r2, 2.0);
	float d3 = pow((pos.x - pos3.x), 2.0) + pow((pos.y - pos3.y), 2.0) - pow(r3, 2.0);

	float dist = abs (stickiness - d1 * d2 * d3);
	vec3 position = vec3(pos.x, pos.y, dist);

	float d12 = distance(pos1, pos2);
	float d13 = distance(pos1, pos3);
	float d23 = distance(pos2, pos3);
	float weght12 = 1.0 / (1.0 + exp(((distance(pos1, pos) / d12) - 0.5) * 8.0));
	float weght13 = 1.0 / (1.0 + exp(((distance(pos1, pos) / d13) - 0.5) * 8.0));
	float weght23 = 1.0 / (1.0 + exp(((distance(pos2, pos) / d23) - 0.5) * 8.0));

	vec2 centre12 = (weght12 * pos1) + ((1.0 - weght12) * pos2);
	vec2 centre13 = (weght13 * pos1) + ((1.0 - weght13) * pos3);
	vec2 centre = (weght23 * centre12) + ((1.0 - weght23) * centre13);

	highp float up = pow(dist, 0.45);
	vec3 normal = normalize(vec3(pos.x - centre.x, pos.y - centre.y, up));

	vec4 colour;
	colour.xyz = BlinnPhongLighting (position, vec3(0.0, 0.0, 1.0), normal);
	colour.a = 1.0;

	gl_FragColor = colour;
}

